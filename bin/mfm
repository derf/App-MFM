#!/usr/bin/env perl
use strict;
use warnings;
use 5.010;

use App::MFM::PluginLoader;
use Curses::UI;
use Cwd;

our $VERSION = '0.00';

my ( $cui, $win, $listbox );
my $plugin = App::MFM::PluginLoader->new();
my %cursor_ids;

$plugin->load('Filetype');

sub ls {
	my ($dir) = @_;
	my @ret;

	if ( not -d $dir ) {
		return ();
	}

	opendir( my $dh, $dir ) or die("Cannot open ${dir}: $!\n");

	@ret = grep { $_ !~ / ^ [.] /x } readdir($dh);

	closedir($dh);

	@ret = sort { lc($a) cmp lc($b) } @ret;
	return (@ret);
}

sub annotate {
	my (@files) = @_;
	my $ret;

	for my $file (@files) {
		my $desc = q{};

		for my $loaded ( $plugin->loaded_plugins ) {
			$desc .= $plugin->run( $loaded, $file ) // q{};
		}
		$ret->{"$file"} = sprintf( '%-7s %s', $desc, $file );
	}
	return $ret;
}

sub change_list {
	my ($list) = @_;
	my $sel    = $list->get();
	my $id     = $list->id();

	$cursor_ids{ getcwd() } = $id;

	if ( -d $sel ) {
		$list->clear_selection();
		chdir($sel);
	}

	$win->title( getcwd() );
	$list->values( '..', ls('.') );
	$list->labels( annotate( ls('.') ) );
	$list->draw();

	if ( $cursor_ids{ getcwd() } ) {
		for ( 1 .. $cursor_ids{ getcwd() } ) {
			$list->option_next();
		}
	}

	return;
}

$cui = Curses::UI->new( -clear_on_exit => 1, );

$cui->set_binding( sub { exit 0 }, 'q' );

$win = $cui->add(
	'0', 'Window',
	-title  => getcwd(),
	-border => 1,
);

$listbox = $win->add(
	'foo', 'Listbox',
	-values   => [ '..', ls('.') ],
	-labels   => annotate( ls('.') ),
	-onchange => sub     { change_list( $_[0] ) },
);

$listbox->set_binding( 'option-select',   'n' );
$listbox->set_binding( 'option-next',     't' );
$listbox->set_binding( 'option-prev',     'h' );
$listbox->set_binding( 'option-nextpage', 'T' );
$listbox->set_binding( 'option-prevpage', 'H' );

$listbox->focus();

$cui->mainloop();

__END__

=head1 NAME

=head1 SYNOPSIS

=head1 DESCRIPTION

=head1 OPTIONS

=head1 EXIT STATUS

=head1 CONFIGURATION

=head1 DEPENDENCIES

=head1 BUGS AND LIMITATIONS

=head1 AUTHOR

Copyright (C) 2010 by Daniel Friesel E<lt>derf@finalrewind.orgE<gt>

=head1 LICENSE

  0. You just DO WHAT THE FUCK YOU WANT TO.
